import com.anysoftkeyboard.tools.generatewordslist.GenerateWordsListTask

buildscript {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'

        classpath 'com.github.AnySoftKeyboard.AnySoftKeyboardTools:makedictionary:e0929d04f944eae0154c47e834e42f7dcbcb837c'
        classpath 'com.github.AnySoftKeyboard.AnySoftKeyboardTools:generatewordslist:e0929d04f944eae0154c47e834e42f7dcbcb837c'
        //classpath files('english_dictionary/generatewordslist-1.0-SNAPSHOT.jar')
        classpath 'org.jsoup:jsoup:1.9.1'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "com.anysoftkeyboard.languagepack.tatar"
        minSdkVersion 7
        targetSdkVersion 23
        versionCode 1
        versionName "0.1-alpha"
    }

    signingConfigs {
        release {
            def propsFile = rootProject.file('keystore.properties')

            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }

        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            debuggable false

            minifyEnabled false
        }
    }
}

//task parseStateAssembly(type: com.anysoftkeyboard.tools.generatewordslist.GenerateWordsListTask) {
//    inputFile new File(project.getProjectDir(), "dictionary/kitaphane.tatarstan.ru.united.txt")
//    outputWordsListFile new File(project.getProjectDir(), "dictionary/words_ny_state_assembly.xml")
//}
def tatarCharacters = "АаБбВвГгДдЕеЁёЖжЗзИиЙйКкЛлМмНнОоПпРрСсТтУуФфХхЦцЧчШшЩщЪъЫыЬьЭэЮюЯя".toCharArray()
def additionalTatarInnerCharacters = "-".toCharArray();

task parseKitaphaneTatarstanRu(type: GenerateWordsListTask) {
    inputFile new File(project.getProjectDir(), "dictionary/kitaphane.tatarstan.ru.united.txt")
//    wordCharacters = "АаӘәБбВвГгДдЕеЁёЖжҖҗЗзИиЙйКкЛлМмНнҢңОоӨөПпРрСсТтУуҮүФфХхҺһЦцЧчШшЩщЪъЫыЬьЭэЮюЯя".toCharArray()
    wordCharacters = tatarCharacters
    additionalInnerCharacters = additionalTatarInnerCharacters
//    locale new Locale("ru")
    outputWordsListFile new File(project.getProjectDir(), "dictionary/kitaphane.tatarstan.ru.united.xml")
}

task mergeAllWordLists(type: com.anysoftkeyboard.tools.generatewordslist.MergeWordsListTask) {
//    dependsOn parseStateAssembly
    dependsOn parseKitaphaneTatarstanRu

    inputWordsListFiles = [
//            new File(project.getProjectDir(), "dictionary/words_ny_state_assembly.xml"),
//            new File(project.getProjectDir(), "dictionary/popular_websites_words.xml"),//website I use most
new File(project.getProjectDir(), "dictionary/kitaphane.tatarstan.ru.united.xml")
    ] as File[]
    outputWordsListFile new File(project.getProjectDir(), "dictionary/words_merged.xml")
    maxWordsInList 100000
}

task makeDictionary(type: com.anysoftkeyboard.tools.makedictionary.MakeDictionaryTask) {
    dependsOn mergeAllWordLists
    inputWordsListFile new File(project.getProjectDir(), "dictionary/words_merged.xml")
}

afterEvaluate { proj ->
    proj.tasks.all { task ->
        if (task.name.startsWith('generate') && task.name.endsWith('BuildConfig')) {
            task.dependsOn makeDictionary
        }
    }
}

dependencies {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    compile 'com.github.AnySoftKeyboard:AnySoftKeyboard-API:1.3.5'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
